#= depend_on_asset 'alert.html'
#= depend_on_asset 'home.html'
#= depend_on_asset 'login.html'
#= depend_on_asset 'sign_up.html'
#= depend_on_asset 'dashboard.html'
#= depend_on_asset 'meal.html'

app = angular.module 'caloriesApp', ['ngRoute', 'ui.bootstrap']

app.factory 'alerts', ['$rootScope', ($rootScope) ->
  $rootScope.alerts = []

  alerts =
    addAlert: (type, msg) ->
      $rootScope.alerts.push
        type: type
        msg: msg
        close: ->
          alerts.closeAlert(@)
    closeAlert: (alert) ->
      @closeAlertAtIdx $rootScope.alerts.indexOf(alert)
    closeAlertAtIdx: (idx) ->
      $rootScope.alerts.splice idx, 1
    clearAlerts: ->
      $rootScope.alerts = []
]

app.config ['$routeProvider', '$locationProvider', ($routeProvider, $locationProvider) ->
  $locationProvider.html5Mode true

  $routeProvider.when '/', templateUrl: "<%= asset_path('home.html') %>"
  $routeProvider.when '/login', templateUrl: "<%= asset_path('login.html') %>"
  $routeProvider.when '/sign_up', templateUrl: "<%= asset_path('sign_up.html') %>"
  $routeProvider.when '/dashboard', templateUrl: "<%= asset_path('dashboard.html') %>", auth: true
  $routeProvider.when '/meals/new', templateUrl: "<%= asset_path('meal.html') %>", controller: 'MealCtrl', auth: true
  $routeProvider.when '/meals/:mealId/edit', templateUrl: "<%= asset_path('meal.html') %>", controller: 'MealCtrl', auth: true
]

app.run ['$rootScope', '$location', 'alerts', ($rootScope, $location, alerts) ->
  $rootScope.currentUser = exports.currentUser

  $rootScope.$on "$routeChangeStart", (event, next, current) ->
    if (next.auth and not $rootScope.currentUser)
      event.preventDefault()
      $location.path('/login')
    else if (not next.auth and $rootScope.currentUser)
      event.preventDefault()
      $location.path('/dashboard')
    else
      alerts.clearAlerts()
]

app.controller 'ApplicationCtrl', ['$rootScope', '$scope', '$http', '$location', ($rootScope, $scope, $http, $location) ->
  busy = false

  $scope.logout = ->
    return if busy
    busy = true
    $http.delete('/api/users/sign_out')
      .success (data) ->
        $rootScope.currentUser = null
        $location.path("/")
        busy = false
      .error ->
        console.log 'error'
        busy = false
]

app.controller 'LoginCtrl', ['$scope', '$http', '$location', 'alerts', '$rootScope', ($scope, $http, $location, alerts, $rootScope) ->
  $scope.credentials =
    email: ""
    password: ""

  busy = false

  $scope.login = (credentials) ->
    return if busy
    busy = true
    $http.post('/api/users/sign_in', user: credentials)
      .success (data) ->
        busy = false
        $rootScope.currentUser = data
        $location.path('/dashboard')
      .error ->
        console.log 'error'
        alerts.addAlert 'danger', 'Invalid email or password.'
        busy = false
]
